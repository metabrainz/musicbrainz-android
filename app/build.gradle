plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
    compileSdk 32
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "org.metabrainz.android"
        minSdk 21
        targetSdk 32
        versionCode 45
        versionName "5.0.4"

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            // proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        String sharedTestResourcesDir = 'src/sharedTest/resources'

        test {
            java.srcDirs += [sharedTestDir]
            resources.srcDirs += [sharedTestResourcesDir]
        }

        androidTest {
            java.srcDirs += [sharedTestDir]
            resources.srcDirs += [sharedTestResourcesDir]
        }
    }
    lint {
        abortOnError false
        disable 'MissingTranslation'
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    // Web Service Setup
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.4'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.4'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.0'

    //Image downloading and Caching library
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    kapt 'com.github.bumptech.glide:compiler:4.13.0'

    //Fragment Setup For Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'

    //Test Setup
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:5.0.0-alpha.4'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0-native-mt'

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'

    //Tagger & Metadata Setup
    implementation 'info.debatty:java-string-similarity:2.0.0'
    implementation 'com.github.amcap1712:KTaglib:release-SNAPSHOT'

    //Handling WebViews
    implementation 'com.thefinestartist:finestwebview:1.2.7'

    //Design Setup
    implementation 'com.google.android.material:material:1.6.0-alpha02'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.airbnb.android:lottie:4.2.2'
    implementation 'com.github.akshaaatt:Onboarding:1.0.3'

    //Barcode Scan
    implementation 'me.dm7.barcodescanner:zbar:1.9.13'

    //Instant App
    implementation 'com.google.android.gms:play-services-instantapps:18.0.1'

    //Dagger-Hilt
    implementation("com.google.dagger:hilt-android:$hilt_version")
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")
    implementation group: 'androidx.lifecycle', name: 'lifecycle-viewmodel-ktx', version: '2.4.1'
    kapt("androidx.hilt:hilt-compiler:1.0.0")
}
